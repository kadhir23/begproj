#include <OneWire.h>
#include <DallasTemperature.h>
#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C lcd(0x27, 16, 2);

#define ONE_WIRE_BUS 2
#define TDS_PIN A0
#define VREF 5.0
#define SCOUNT 30

OneWire oneWire(ONE_WIRE_BUS);
DallasTemperature sensors(&oneWire);

int analogBuffer[SCOUNT];
int analogBufferTemp[SCOUNT];
int bufferIndex = 0;

void setup() {
  Serial.begin(9600);
  sensors.begin();
  lcd.init();
  lcd.backlight();

  lcd.setCursor(0,0);
  lcd.print("Underwater Mon");
  lcd.setCursor(0,1);
  lcd.print("System Booting");
  delay(1500);
  lcd.clear();
}

void loop() {

  sensors.requestTemperatures();
  float temperature = sensors.getTempCByIndex(0);

  analogBuffer[bufferIndex] = analogRead(TDS_PIN);
  bufferIndex++;
  if (bufferIndex == SCOUNT) bufferIndex = 0;

  for (int i = 0; i < SCOUNT; i++) analogBufferTemp[i] = analogBuffer[i];
  int median = getMedianNum(analogBufferTemp, SCOUNT);

  float voltage = median * (float)VREF / 1024.0;

  float compensationCoefficient = 1.0 + 0.02 * (temperature - 25.0);
  float compensatedVoltage = voltage / compensationCoefficient;

  float tdsValue = (133.42 * compensatedVoltage * compensatedVoltage * compensatedVoltage
                   - 255.86 * compensatedVoltage * compensatedVoltage
                   + 857.39 * compensatedVoltage) * 0.5;

  lcd.setCursor(0,0);
  lcd.print("Temp:");
  lcd.print(temperature);
  lcd.print("C  ");

  lcd.setCursor(0,1);
  lcd.print("TDS :");
  lcd.print(tdsValue);
  lcd.print("ppm ");

  delay(500);
}

int getMedianNum(int bArray[], int length) {
  int temp;
  for (int i = 0; i < length - 1; i++) {
    for (int j = i + 1; j < length; j++) {
      if (bArray[i] > bArray[j]) {
        temp = bArray[i];
        bArray[i] = bArray[j];
        bArray[j] = temp;
      }
    }
  }
  if ((length & 1) > 0)
    temp = bArray[(length - 1) / 2];
  else
    temp = (bArray[length / 2] + bArray[length / 2 - 1]) / 2;

  return temp;
}
